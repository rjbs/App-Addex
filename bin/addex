#!/usr/bin/perl
use strict;
use warnings;

package App::Addex::addex;
# PODNAME:  addex
# ABSTRACT: create mail helper files for address book entries

=head1 DESCRIPTION

The F<addex> command produces configuration for various email tools based on
your address book.  For more information, consult L<App::Addex>.

=cut

use App::Addex;
use Config::MVP::Assembler;
use Config::MVP::Reader::INI;
use File::HomeDir;
use File::Spec;
use Getopt::Long::Descriptive;

my ($opt, $usage) = describe_options(
  '%c %o',
  [ 'config|c=s',     'config file name',
    { default => File::Spec->catfile(File::HomeDir->my_home, '.addex') }   ],
);

my $assembler = Config::MVP::Assembler->new;
$assembler->change_section('App::Addex', 'classes');

my $seq = Config::MVP::Reader::INI->read_config(
  $opt->{config},
  { assembler => $assembler }
);

my $addex = App::Addex->from_sequence($seq);

$addex->run;

